plugins {
  // To create a fat jar build/libs/bcel-util-all.jar, run: ./gradlew shadowJar
  id "com.github.johnrengelman.shadow" version "2.0.4"
  // Code formatting with Google Java Format; use targets "goJF" and "verJF"
  id "com.github.sherter.google-java-format" version "0.7.1"
  // To show task list as a tree, run: ./gradlew <taskname> taskTree
  id "com.dorongold.task-tree" version "1.3"
  // Run the Error Prone linter.
  id "net.ltgt.errorprone-base" version "0.0.16"
}

apply plugin: 'java-library'

// Checker Framework pluggable type-checking
apply from: "${buildscript.sourceFile.parent}/gradle/checkerframework.gradle"
// To upload to Maven Central, see instructions in this file.
apply from: "${buildscript.sourceFile.parent}/gradle/mavencentral.gradle"

dependencies {
  api group: 'org.checkerframework.annotatedlib', name: 'bcel', version: '6.2.0.1'
  errorprone 'com.google.errorprone:error_prone_core:2.3.1'
  testImplementation 'junit:junit:4.12'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  // This cannot be enabled because StackVer.java lacks some documentation.
  // options.addStringOption('private', '-quiet')
}

task javadocWeb(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/bcel-util/api")
  classpath = project.sourceSets.main.compileClasspath
}

task installGitHooks(type: Copy) {
  description 'Copies git hooks to .git directory'
  from 'githooks'
  into '.git/hooks'
}

task compileAll() {
  dependsOn compileJava
  dependsOn compileTestJava
}

tasks.withType(JavaCompile) {
  dependsOn(':installGitHooks')
}
