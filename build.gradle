plugins {
  // Apply the java-library plugin to add support for Java Library
  id 'java-library'

  id 'com.github.sherter.google-java-format' version '0.6'

}

// maven-publish does no signing, so it cannot be used with Maven Central.
// apply plugin: 'maven-publish'

dependencies {
  api group: 'org.apache.bcel', name: 'bcel', version: '6.0'

  implementation 'org.apache.commons:commons-math3:3.6.1'
  implementation 'com.google.guava:guava:20.0'

  testImplementation 'junit:junit:4.12'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


///////////////////////////////////////////////////////////////////////////
/// Code formatting
///

googleJavaFormat {
  toolVersion '1.3'
  // exclude '**/CloneVisitor.java'
}

tasks.googleJavaFormat {
  group 'Formatting'
  description = "Reformat Java source code with Google-Java-format"
  exclude 'src/testinput'
}

tasks.verifyGoogleJavaFormat {
  group 'Formatting'
  description = "Check Java source code formatting with Google-Java-format"
  exclude 'src/testinput'
}

// Run googleJavaFormat before compiling (would be better to just verify?)
tasks.withType(JavaCompile) {
  dependsOn 'googleJavaFormat' // task name conflicts with plugin name
}


///////////////////////////////////////////////////////////////////////////
/// Maven Central publishing
// (From http://central.sonatype.org/pages/gradle.html )

apply plugin: 'maven'
apply plugin: 'signing'

group = "org.plumelib"
archivesBaseName = "bcel-util"
// When editing this, also increment in README.md (when it exists).
version = "0.0.3"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Plume-lib Bcel-Util'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Utility functions for BCEL.'
        url 'https://github.com/plume-lib/bcel-util'

        scm { 
          connection 'scm:git:git@github.com:plume-lib/bcel-util.git'
          developerConnection 'scm:git:git@github.com:plume-lib/bcel-util.git'
          url 'git@github.com:plume-lib/bcel-util.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'mernst'
            name 'Michael Ernst'
            email 'mernst@alum.mit.edu'
          }
        }
      }
    }
  }
}
